import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.0'
    id 'org.graalvm.buildtools.native' version '0.11.3'
}

group = 'io.github.onu_eccs1621_sp2025.westward'
version = '1.0.0-beta.2'

project.ext.lwjglVersion = "3.3.6"

repositories {
    mavenCentral()
}

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if  (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if  (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

dependencies {
    implementation "io.github.spair:imgui-java-app:1.90.0"
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.slf4j:slf4j-simple:2.0.17'

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': 'io.github.onu_eccs1621_sp2025.westward.TrailApplication'
        )
    }
    archiveFileName = 'westward.jar'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}


javadoc {
    source = sourceSets.main.allJava
}

graalvmNative {
    toolchainDetection = true

    binaries {
        main {
            imageName = 'westward'
            mainClass = 'io.github.onu_eccs1621_sp2025.westward.TrailApplication'
            debug = false
            verbose = false
            fallback = false
            sharedLibrary = false
            quickBuild = false
            richOutput = true

            configurationFileDirectories.from(file('src/main/resources/META-INF'))

            // Advanced options
            buildArgs.add('--link-at-build-time')

            useFatJar = true
        }
    }
}
